plugins {
	id 'com.android.dynamic-feature'
}

// TODO Try to reuse from base gradle
ext.readProperty = { paramName -> readPropertyWithDefault(paramName, null) }
ext.readPropertyWithDefault = { paramName, defaultValue ->
	if (project.hasProperty(paramName)) {
		return project.getProperties().get(paramName)
	} else {
		Properties properties = new Properties()
		if (project.rootProject.file('local.properties').exists()) {
			properties.load(project.rootProject.file('local.properties').newDataInputStream())
		}
		if (properties.getProperty(paramName) != null) {
			return properties.getProperty(paramName)
		} else {
			return defaultValue
		}
	}
}

android {
	compileSdkVersion 30
	buildToolsVersion "29.0.3"

	defaultConfig {
		minSdkVersion 23
		targetSdkVersion 30
		versionCode 1120
		versionName "1.1.2"
		resConfigs "en", "fr", "de"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	productFlavors {
		dev {
			applicationIdSuffix '.dev'
		}
		prod {
		}
		abn {
			applicationIdSuffix '.abn'
		}
	}

	flavorDimensions "version"

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	buildFeatures {
		buildConfig = false
	}
}


dependencies {
	implementation project(":base")

	implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'

	// QR code scanning
	def camerax_version = "1.0.0-rc01"
	implementation "androidx.camera:camera-camera2:${camerax_version}"
	implementation "androidx.camera:camera-lifecycle:${camerax_version}"
	implementation "androidx.camera:camera-view:1.0.0-alpha20"
	//DO NOT UPDATE ZXING TO 3.4.0! (3.4.0 is not compatible with API Level 23 or lower)
	implementation "com.google.zxing:core:3.3.0"

	implementation 'com.squareup.retrofit2:retrofit:2.9.0'
	implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
	implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
	implementation 'androidx.security:security-crypto:1.0.0-rc03'
	implementation 'androidx.biometric:biometric:1.1.0-rc01'

}